#!/usr/bin/env perl

use strict;
use warnings;
use utf8;

use FindBin;
use lib "$FindBin::Bin/local/lib/perl5";

use Cwd qw(realpath getcwd);
use File::Path qw(mkpath);
use IO::File;
use IPC::Open3 qw(open3);
use JSON::PP qw(encode_json);
use Data::Dumper;
use Digest::MD5;
use Digest::SHA;
use Getopt::Long;
use HTTP::Tiny;
use Config::Tiny;
use YAML::Tiny qw(LoadFile Dump);
use File::Basename qw(basename fileparse dirname);
use File::Spec::Functions qw(catfile);
use File::Temp qw(tempfile tmpnam tempdir);

GetOptions(
    'recipe=s'  => \my $recipes,
    'repo=s'    => \my $repos,
    'pr=s'      => \my $pull_reqs,
    'archive=s' => \my $archives,
);

my $recipe_dir    = realpath("$FindBin::Bin/../recipe");
my $repo_dir      = realpath("$FindBin::Bin/../repo");
my $repo_base_dir = realpath("$FindBin::Bin/../..");
my $archive_dir   = realpath("$FindBin::Bin/../archive");
my $git_clone_cmd = realpath("$FindBin::Bin/git-clone");

my $env       = {};
my $yamls     = [];
my $workspace = {
    folders  => [],
    settings => {},
};
my $pr_is_used;

sub _check_integrity {
    my ($integrity, $file_or_content) = @_;

    my ($algorithm) = $integrity =~ m{\Asha(\d+)};
    my $sha = Digest::SHA->new($algorithm);
    if (ref $file_or_content) {

        $sha->addfile($file_or_content);
    } else {
        $sha->add($file_or_content);
    }
    my $digest = $sha->b64digest;
    $digest .= ('=' x ((4 - (length($digest) % 4)) % 4));
    my $file_integrity = "sha256-" . $digest;

    return $integrity eq ("sha$algorithm-" . $digest);
}

sub handle_recipe {
    my $data = shift;

    # detect docker images
    if ( my $perl = delete $data->{perl} ) {
        $env->{DOCKER_MT_IMAGE} = "movabletype/test:perl-$perl";
    }
    if ( my $mysql = delete $data->{database} ) {
        $env->{DOCKER_MYSQL_IMAGE} = $mysql;
    }
    if ( my $php = delete $data->{php} ) {
        $env->{DOCKER_HTTPD_IMAGE} = "movabletype/test:php-$php";
    }

    # checout branch and collect links
    my @volumes;
    for my $k ( keys %$data ) {
        if (my $archive = $data->{$k}{archive}) {
            my $url       = $archive->{url};
            my $integrity = $archive->{integrity};

            die "archive url and integrity is required: $k"
                unless $url && $integrity;

            my $basename  = basename($url);
            my $rel_path  = catfile($k,           $basename);
            my $full_path = catfile($archive_dir, $rel_path);

            if (-f $full_path && _check_integrity($integrity, IO::File->new($full_path, 'r'))) {
                $archives = "$rel_path $archives";
                next;
            }

            mkpath(dirname($full_path));
            my $response = HTTP::Tiny->new->get($url);
            die "Failed: @{[Dumper($response)]}"
                unless $response->{success} && length $response->{content};

            if (!_check_integrity($integrity, $response->{content})) {
                die "intergrity check failed: $url";
            }

            my $fh = IO::File->new($full_path, 'w')
                or die "Can't open $full_path: $!";
            print $fh $response->{content};
            close $fh;

            $archives = "$rel_path $archives";

            next;
        }

        my $repo     = $data->{$k}{location};
        if (($ENV{CODESPACES} || '') eq 'true') {
            $repo =~ s{^git\@github.com:}{https://github.com/};
        }
        my $dest_dir = $data->{$k}{directory} || do {
            "$repo_base_dir/@{[basename($repo)]}";
        };
        if ( $dest_dir !~ m{\A/} ) {
            $dest_dir = "$repo_base_dir/$dest_dir";
        }
        ( my $basename = $dest_dir ) =~ s{.*/}{};

        if ( $k eq 'core' ) {
            $env->{MT_HOME_PATH} = $dest_dir;
        }

        if ($repo) {
            my $branch = $data->{$k}{branch} || '';

            for my $pr ( split /\s+/, $pull_reqs ) {
                my ( $pr_repo, $pr_id ) = ( $pr =~ m{([^/]+)/(pull/\d+)} );
                next unless $pr_repo && $pr_id;
                if ($basename eq $pr_repo) {
                    $branch     = $pr_id . '/head';
                    $pr_is_used = 1;
                }
            }

            {
                my %repos = map { $_ => 1 } split /\s+/, $repos;
                for my $r ( keys %repos ) {
                    my ( $bn, $br ) = $r =~ m{([^/]+)#(.*)$}
                        or next;

                    next unless $basename eq $bn;

                    $branch = $br;
                    delete $repos{$r};
                }
                $repos = join ' ', keys %repos;
            }

            my $cmd = "$git_clone_cmd $repo $dest_dir $branch";
            my $res = `$cmd 2>&1` || "";
            die "$res\nGot an error: $cmd" if $?;

            $res = `cd $dest_dir && git rev-parse HEAD 2>&1`;
            die "$res\nGot an error: git rev-parse HEAD" if $?;
            print STDERR "$k:$res";

            my $build_cmd = $data->{$k}{build}{command};
            if ($build_cmd) {
                my $cur_dir = getcwd();
                chdir $dest_dir;
                my ($res, $build_cmd_str);
                if (ref $build_cmd eq 'ARRAY') {
                    my $pid = open3( my $in, my $out, my $err, @$build_cmd );
                    $res .= join('', <$out>) if $out;
                    $res .= join('', <$err>) if $err;
                    waitpid($pid, 0);
                    $build_cmd_str = join(' ', @$build_cmd);
                }
                else {
                    $res = `$build_cmd 2>&1`;
                    $build_cmd_str = $build_cmd;
                }
                chdir $cur_dir;
                die "$res\nGot an error: $build_cmd_str" if $?;
            }
        }

        push @volumes, "      - '$dest_dir:/src/$basename:\${DOCKER_VOLUME_MOUNT_FLAG:-rw}'";
        push @{ $workspace->{folders} }, { path => $basename, };

        next if $k eq 'core';

        my $plugin_dir = $dest_dir;
        if ( $data->{$k}{prefix} ) {
            $plugin_dir = catfile( $plugin_dir, $data->{$k}{prefix} );
        }

        my $links = $data->{$k}{links};
        if ( !$links ) {
            $links = [
                map { $_ =~ s{\A\Q$plugin_dir\E/}{}; $_ } (
                    glob("$plugin_dir/*.cgi"),
                    glob("$plugin_dir/plugins/*"),
                    glob("$plugin_dir/mt-static/plugins/*"),
                    glob("$plugin_dir/themes/*"),
                    glob("$plugin_dir/addons/*"),
                    glob("$plugin_dir/mt-static/addons/*"),
                    glob("$plugin_dir/tools/*"),
                )
            ];
        }

        for my $l (@$links) {
            next
              if $ENV{BIND_MOUNT_DIRECTORIES_ONLY} eq 'yes'
              && !-d "$plugin_dir/$l";
            push @volumes,
                "      - '$plugin_dir/$l:/var/www/cgi-bin/mt/$l:\${DOCKER_VOLUME_MOUNT_FLAG:-rw}'";
        }
    }

    return unless @volumes;

    my ( $fh, $file ) = tmpnam();
    print $fh <<YAML;
services:
  mt:
    volumes:
@{[join "\n", @volumes]}
  httpd:
    volumes:
@{[join "\n", @volumes]}
  mt-watcher:
    volumes:
@{[join "\n", @volumes]}
YAML
    close $fh;

    push @$yamls, $file;
}

for my $recipe ( split /\s+/, $recipes ) {
    my $recipe_file
        = ( $recipe =~ m{^[/\.]} && -e $recipe )
        ? realpath($recipe)
        : do {
        $recipe =~ s{\.yaml\z}{};
        $recipe .= '.yaml';
        "$recipe_dir/$recipe";
        };

    # get recipe
    if ( !-e $recipe_file ) {
        $recipe_file = "";

        my $recipe_url = $recipe =~ m{\Ahttps?://} ? $recipe : do {
            my $conf_file = catfile( $ENV{'HOME'}, '.mt-dev.conf' );

            my $conf = Config::Tiny->read( $conf_file, 'utf8' );
            if ( !$conf ) {
                $conf = Config::Tiny->new;

                print STDERR "Please input base URL of recipe data: ";
                my $base = <STDIN>;
                chomp $base;

                $conf->{recipe}{base_url} = $base;
                $conf->write( $conf_file, 'utf8' );
            }

            if ( $conf->{recipe}{base_url} ) {
                my $base_url = $conf->{recipe}{base_url};
                $base_url =~ s/\/+$//;
                "$base_url/$recipe";
            }
            else {
                "";
            }
        };

        if ($recipe_url) {
            my $response = HTTP::Tiny->new->get($recipe_url);
            die "Failed: @{[Dumper($response)]}"
                unless $response->{success} && length $response->{content};

            my ( $fh, $filename ) = tempfile();
            print $fh $response->{content};

            $recipe_file = $filename;
        }

        if ( !$recipe_file ) {
            die qq{Can not find recipe for "$recipe".};
        }
    }

    handle_recipe( LoadFile($recipe_file) );
}

for my $r ( split /\s+/, $repos ) {
    my $directory
        = ( $r =~ m{^[/\.]} && -d $r )
        ? realpath($r)
        : "$repo_dir/$r";

    next unless $directory;

    # get repo
    if ( !-d $directory ) {
        my $branch = '';
        $branch = $1 if $r =~ s{(?:#([\w/-]+))\z}{};

        my $k = basename($r);
        $directory = catfile( $repo_base_dir, $k );

        my $cmd = "$git_clone_cmd $r $directory $branch";
        my $res = `$cmd 2>&1` || "";
        die "$res\nGot an error: $cmd" if $?;

        $res = `cd $directory && git rev-parse HEAD 2>&1`;
        die "$res\nGot an error: git rev-parse HEAD" if $?;
        print STDERR "$k:$res";
    }

    handle_recipe( { $r => { directory => $directory, }, } );
}

for my $a ( split /\s+/, $archives ) {
    next unless $a;

    my $file_path;
    my ( $basename, $ext ) = ( fileparse( $a, qr/\.[a-zA-Z\.]+$/ ) )[ 0, 2 ];

    if ( $a =~ m{^https?://} ) {

        # download
        my $response = HTTP::Tiny->new->get($a);

        if (
            $response->{status} == 404
            && system('gh auth status > /dev/null 2>&1') == 0
            && $a =~ m{^https://github.com/([^/]+/[^/]+)/actions/runs/[0-9]+/artifacts/([0-9]+)$}
        ) {
            my $repo = $1;
            my $artifact_id = $2;

            my $cwd     = getcwd();
            my $tmp_dir = tempdir( '/tmp/mt-dev-archive-temp-XXXXX', );

            chdir $tmp_dir;

            my $cmd = "gh api repos/$repo/actions/artifacts/$artifact_id/zip > archive.zip";
            my $res = `$cmd 2>&1` || "";
            die "$res\nGot an error: $cmd" if $?;

            $cmd = "unzip -q archive.zip";
            $res = `$cmd 2>&1` || "";
            die "$res\nGot an error: $cmd" if $?;

            unlink 'archive.zip';
            my ($filename) = `ls`;
            chomp $filename;

            chdir $cwd;

            $file_path = catfile( $tmp_dir, $filename );
        }
        elsif ( !$response->{success} || !$response->{content} ) {
            die "Failed: @{[Dumper($response)]}";
        }
        else {
            ( my $fh, $file_path ) = tempfile( undef, SUFFIX => $ext );
            print $fh $response->{content};
        }
    }
    elsif ( $a =~ m{^/} ) {
        $file_path = $a;
    }
    else {
        $file_path = catfile( $archive_dir, $a );
    }

    die "File not found: $a" unless -f $file_path;

    # extract archive
    my $cwd       = getcwd();
    my $directory = tempdir( '/tmp/mt-dev-archive-temp-XXXXX', );

    chdir $directory;
    my $extract_res
        = $file_path =~ m/\.zip$/i
        ? `unzip -q $file_path`
        : `tar zxf $file_path`;
    if ($?) {
        die "Failed to extract: $a";
    }

    my $md5_ctx = Digest::MD5->new;
    $md5_ctx->addfile( IO::File->new( $file_path, 'r' ) );
    print STDERR join( ':', $basename . $ext, $md5_ctx->hexdigest ) . "\n";

    my @entries = grep {
        !m{^(addons|mt-static|php|alt-tmpl|plugins|default_templates|search_templates|extlib|themes|import|tmpl|tools|lib|.*\.cgi)$}
    } glob('*');
    if ( @entries == 1 ) {
        $directory = catfile( $directory, $entries[0] );
    }
    chdir $cwd;

    $basename =~ s{\.}{_}g;    # "." causes problems in yaml files
    handle_recipe( { $basename => { directory => $directory, }, } );
}

if ($pull_reqs && !$pr_is_used) {
    die "specified pr option is not used: ${pull_reqs}";
}

{
    my ( $ws_fh, $ws_file ) = tmpnam();
    print $ws_fh encode_json($workspace);
    close $ws_fh;

    my ( $fh, $file ) = tmpnam();
    print $fh Dump({
        services => {
            map {
                $_ => { volumes => ["$ws_file:/src/mt.code-workspace",], },
            } qw(mt httpd)
        },
    });
    push @$yamls, $file;
}

if (@$yamls) {
    $env->{DOCKER_COMPOSE_YAML_OVERRIDE} = join( ' ', map {"-f $_"} @$yamls );
}

# output args for make command
print join ' ', map {qq{$_="@{[$env->{$_}]}"}}
    keys(%$env);
